[gd_scene load_steps=3 format=3 uid="uid://bc5cf0tfrf6cq"]

[sub_resource type="GDScript" id="GDScript_q3d7w"]
script/source = "extends Control

enum PLOT_OPTION {
	EOT,
	Sunset,
	Sunrise,
	TwilightDuration,
	Declination
}

const DEFAULT_NUM_TICKS := 15
const GRAPH_BORDER_THICKNESS := 3
const VERTICAL_PLOT_MARGIN := 0.05
const TIKS_NUM_DECIMALS := 2

@export var plot_option := PLOT_OPTION.EOT :
	set(value):
		plot_option = value
		if plots_anchor:
			_plot_selected_eot_values()

var current_plot_rect_pos : Vector2
var current_plot_rect_size : Vector2

var zero_plot_line : Line2D

var full_eot_infos : Array

@onready var plots_anchor = %PlotsBackground
@onready var plot_outline = %PlotOutline
@onready var x_ticks_anchor = %XTicks
@onready var y_ticks_anchor = %YTicks
@onready var y_ticks_margin = %Margin
@onready var x_label = %XLabel
@onready var y_label = %YLabel
@onready var plot_option_dropdown = %PlotOptionDropdown


func _ready() -> void:
	for option in PLOT_OPTION.keys().size():
		plot_option_dropdown.add_item(PLOT_OPTION.keys()[option], option)
	
	full_eot_infos = $CustomEOT.get_all_values_till(365)
	
	# Wait till everything has settled before 1st draw
	await get_tree().process_frame
	
	_plot_selected_eot_values()


func _plot_selected_eot_values() -> void:
	var values = []
	var plotted_y_label = \"EOT\\n[planet min]\"
	
	for i in range(full_eot_infos.size()):
		match (plot_option):
			PLOT_OPTION.EOT:
				values.append(full_eot_infos[i][\"eot_vector\"])
			PLOT_OPTION.Sunrise:
				values.append(Vector2(i, full_eot_infos[i][\"sunrise\"]))
			PLOT_OPTION.Sunset:
				values.append(Vector2(i, full_eot_infos[i][\"sunset\"]))
			PLOT_OPTION.TwilightDuration:
				values.append(Vector2(i, full_eot_infos[i][\"twilight_duration\"]))
			PLOT_OPTION.Declination:
				values.append(Vector2(i, full_eot_infos[i][\"declination\"]))
	
	match (plot_option):
		PLOT_OPTION.Sunrise:
			plotted_y_label = \"Sunrise\\n[planet min]\"
		PLOT_OPTION.Sunset:
			plotted_y_label = \"Sunset\\n[planet min]\"
		PLOT_OPTION.TwilightDuration:
			plotted_y_label = \"Sunrise\\n[planet min]\"
		PLOT_OPTION.Declination:
			plotted_y_label = \"Declination\\n[deg]\"
	
	_plot_vectors(values, 3, true, true, false, \"Days since Jan 1\", plotted_y_label)


func _plot_vectors(_vectors : PackedVector2Array, _line_thickness : float = 3, \\
		_clear_plots : bool = true, _round_x_ticks : bool = false, _round_y_ticks : bool = false, \\
		_x_label_text : String = \"\", _y_label_text : String = \"\") -> void:
	
	var x_values = []
	var y_values = []
	
	for i in range(_vectors.size()):
		x_values.append(_vectors[i].x)
		y_values.append(_vectors[i].y)
	
	_plot_values(x_values, y_values, _line_thickness, _clear_plots, _round_x_ticks, \\
			_round_y_ticks, _x_label_text, _y_label_text)


func _plot_values(_x_values : Array, _y_values : Array, _line_thickness : float = 3, \\
		_clear_plots : bool = true, _round_x_ticks : bool = false, _round_y_ticks : bool = false, \\
		_x_label_text : String = \"\", _y_label_text : String = \"\", _ignore_nans : bool = true) -> void:
	
	assert(_x_values.size() == _y_values.size())
	
	for plot in plots_anchor.get_children():
		plot.queue_free()
	
	if zero_plot_line:
		zero_plot_line.queue_free()
		zero_plot_line = null
	
	var x_min = _x_values.min()
	var x_max = _x_values.max()
	
	var y_min = _y_values.min()
	var y_max = _y_values.max()
	
	if _ignore_nans and (is_nan(x_min) or is_nan(x_max)):
		printerr(\"Could not plot because either the min or max x-values are NAN.\")
		return
	if _ignore_nans and (is_nan(y_min) or is_nan(y_max)):
		printerr(\"Could not plot because either the min or max y-values are NAN.\")
		return
	
	_add_ticks_to_anchor(x_ticks_anchor, true, x_min, x_max, DEFAULT_NUM_TICKS, _round_x_ticks)
	_add_ticks_to_anchor(y_ticks_anchor, false, y_min, y_max, DEFAULT_NUM_TICKS, _round_y_ticks)
	
	x_label.text = _x_label_text
	y_label.text = _y_label_text
	
	var plots_rect_size = plots_anchor.size
	
	var min_draw_x = 0
	var max_draw_x = plots_rect_size.x
	
	var min_draw_y = plots_rect_size.y * VERTICAL_PLOT_MARGIN
	var max_draw_y = plots_rect_size.y * (1 - VERTICAL_PLOT_MARGIN)
	
	if y_max > 0 and y_min < 0:
		var zero_y_pos = _find_draw_y_pos(0, y_min, y_max, min_draw_y, max_draw_y)
		zero_plot_line = Line2D.new()
		zero_plot_line.width = _line_thickness
		zero_plot_line.default_color = Color.WHITE
		plots_anchor.add_child(zero_plot_line)
		zero_plot_line.name = \"ZeroPlotLine\"
		zero_plot_line.add_point(Vector2(0, zero_y_pos))
		zero_plot_line.add_point(Vector2(plots_rect_size.x, zero_y_pos))
	
	var plot_line = Line2D.new()
	plot_line.name = \"Plot\"
	plot_line.width = _line_thickness
	plot_line.default_color = Color.BLUE
	plots_anchor.add_child(plot_line)
	var plot_no = plots_anchor.get_child_count() - int(!!zero_plot_line)
	plot_line.name = (\"Plot %d\" % plot_no)
	
	for i in range(_x_values.size()):
		if _ignore_nans and is_nan(_x_values[i]):
			continue
		var x_pos = _find_draw_x_pos(_x_values[i], x_min, x_max, min_draw_x, max_draw_x)
		var y_pos = _find_draw_y_pos(_y_values[i], y_min, y_max, min_draw_y, max_draw_y)
		
		plot_line.add_point(Vector2(x_pos, y_pos))
	
	_draw_graph_border(GRAPH_BORDER_THICKNESS)


func _add_ticks_to_anchor(_ticks_anchor : Control, _horizontal_ticks : bool, _min_value : float, \\
		_max_value : float, _num_ticks : int, _round_ticks : bool = false) -> void:
	
	assert(_ticks_anchor != null)
	
	for child in _ticks_anchor.get_children():
		child.queue_free()
	
	var tick_values = _generate_tick_values(_num_ticks, _min_value, _max_value, _round_ticks)
	
	if !_horizontal_ticks:
		# Invert to draw positive y towards the top
		tick_values.reverse()
		
		# Update vertical margin
#		var y_margin_size = plots_anchor.size.y * VERTICAL_PLOT_MARGIN
#		y_ticks_margin.add_constant_override(\"margin_top\", y_margin_size)
#		y_ticks_margin.add_constant_override(\"margin_bottom\", y_margin_size)
	
	for i in range(tick_values.size()):
		var tick_label = Label.new()
		tick_label.name = \"TickLabel\" + str(i)
		_ticks_anchor.add_child(tick_label)
		if _round_ticks:
			tick_label.text = str(tick_values[i])
		else:
			tick_label.text = str(tick_values[i]).pad_decimals(TIKS_NUM_DECIMALS)
		
		if i < tick_values.size() - 1:
			var spacer = Control.new()
			spacer.name = \"Spacer\" + str(i)
			_ticks_anchor.add_child(spacer)
			if _horizontal_ticks:
				spacer.size_flags_horizontal = SIZE_EXPAND_FILL
			else:
				spacer.size_flags_vertical = SIZE_EXPAND_FILL


func _generate_tick_values(_num_ticks : int, _min_value : float, _max_value : float, \\
		_round_ticks : bool = false) -> PackedFloat32Array:
	
	assert(_max_value > _min_value)
	
	var value_range = _max_value - _min_value
	
	if value_range <= 0:
		_num_ticks = 1
	
	var step_size = float(value_range) / float(_num_ticks)
	
	var ticks = []
	for i in range(_num_ticks + 1):
		var tick_value = _min_value + i * step_size
		if _round_ticks:
			tick_value = int(tick_value) # Or use: stepify(tick_value,0.01)
		ticks.append(tick_value)
	
	return ticks


func _find_draw_x_pos(_value : float, _val_min : float, _val_max : float, _draw_min : float, \\
		_draw_max : float) -> float:
	
	return _transpose_pct_range(_value, _val_min, _val_max, _draw_min, _draw_max) + _draw_min


func _find_draw_y_pos(_value : float, _val_min : float, _val_max : float, _draw_min : float, \\
		_draw_max : float, _ignore_nans : bool = true) -> float:
	
	if (_ignore_nans and is_nan(_value)):
		return 0.0
	
	# Subtract from _draw_max as we want to draw towards negative y
	return _draw_max - _transpose_pct_range(_value, _val_min, _val_max, _draw_min, _draw_max) + _draw_min


func _transpose_pct_range(_value : float, _from_min : float, _from_max : float, _to_min : float, \\
		_to_max : float) -> float:
	
	assert(_value <= _from_max)
	assert(_value >= _from_min)
	
	var from_range = _from_max - _from_min
	var to_range = _to_max - _to_min
	
	var from_pct = (_value - _from_min) / from_range
	
	return to_range * from_pct + _to_min


func _draw_graph_border(_line_thickness : float) -> void:
	if !plot_outline:
		return
	
	var line_offset = _line_thickness / 2.0
	plot_outline.clear_points()
	plot_outline.add_point(plots_anchor.position \\
		+ Vector2(-line_offset, -line_offset))
	plot_outline.add_point(plots_anchor.position \\
		+ Vector2(-line_offset, plots_anchor.size.y + line_offset))
	plot_outline.add_point(plots_anchor.position \\
		+ plots_anchor.size + Vector2(line_offset, line_offset))
	plot_outline.add_point(plots_anchor.position \\
		+ Vector2(plots_anchor.size.x + line_offset, -line_offset))
	plot_outline.add_point(plots_anchor.position \\
		+ Vector2(-line_offset, -line_offset))
	plot_outline.width = _line_thickness
	plot_outline.default_color = Color.WHITE


#func _on_CustomPlotDisplay_resized() -> void:
#	draw_graph_border(GRAPH_BORDER_THICKNESS)
#	if plots_anchor:
#		var new_y_margin_size = plots_anchor.rect_size.y * VERTICAL_PLOT_MARGIN
#		y_ticks_margin.add_constant_override(\"margin_top\", new_y_margin_size)
#		y_ticks_margin.add_constant_override(\"margin_bottom\", new_y_margin_size)
#
#		current_plot_rect_pos = plots_anchor.rect_rotation
#		current_plot_rect_size = plots_anchor.rect_size


func log2(_a : float) -> float:
	return log(_a) / log(2)


func _on_PlotOptionDropdown_item_selected(_index : int) -> void:
	plot_option = _index
"

[sub_resource type="GDScript" id="GDScript_gwx40"]
script/source = "extends Node

## Duration of a real world day: 1 day = 60 * 60 * 24 = 86400 in seconds
const DEFAULT_SECONDS_PER_DAY := 86400.0
## Julian year length in days
const DAYS_IN_JULIAN_CALENDER_YEAR := 365.24
## Approximate and correction the zenit at sunrise/-set of the eath in degrees
const SUNRISE_SUNSET_ZENIT := 90.833
## Difference of sunrise zenit for the of civil twilight in degrees
const CIVIL_TWILIGHT_ZENIT_OFFSET = 6
## The duration of half a day in minutes using the real world's day duration (=DEFAULT_SECONDS_PER_DAY)
const REAL_WORLD_HALF_DAY_MINUTES = 720
## Begin of summertime in pct (based on 27 of March 2022 = Day 87 = Last Sunday in March)
const SUMMER_TIME_BEGIN_PCT = 0.2382
## End of summertime in pct (based on 30 of October 2022 = Day 303 = Last Sunday in October)
const SUMMER_TIME_END_PCT = 0.8296

## Hours per real-world day
const HOURS_PER_DAY := 24.0
## Minutes per real-world hour
const MINUTES_PER_HOUR := 60.0
## Seconds per real-world minute
const SECONDS_PER_MINUTE := 60.0
## Pct of half a day
const HALF_DAY_PCT := 0.5


## How many days a planet year has
@export var days_per_year := DAYS_IN_JULIAN_CALENDER_YEAR
## How many real-world(!) seconds a day has. The actual length of \"planet-seconds\" are determined in
## proportion of a normal day (day_duration / (24 * 60) and might differ from actual seconds
@export var day_duration := DEFAULT_SECONDS_PER_DAY
## Offset that is automatically added to the calculation of each day
## Can be used to start the year at day = 0 but use calculations of a later day
## E.g. For the year to start on 1st of February this variable must be set to of 31 days for year with 365 days
@export var year_start_day_offset := 0.0

## Latitude of the location for these calculations in degrees
@export var latitude := 48.4442
## Longitude of the locationfor these calculations in degrees
@export var longitude := 8.6913

## Aphel (furthest point in the planets orbit) in meters
@export var orbit_dimension_a := 152100000
## Experts only! Perihel (closest point in the planets orbit) in meters
@export var orbit_dimension_b := 147090000
## Experts only! \"Skew\" of the earth axis in degrees
@export var obliquity := -23.45


func get_full_info_about_day(_day : float) -> Dictionary:
	\"\"\"
	Calculates a set of metrics that describe a day of a planet with specified length.
	The metrics include:
		- \"day\": The (actual) day of the input starting from Jan 1st (including the year_start_day_offset)
		- \"year_pct\": The percentage of the day in respect of the year
		- \"eot\": The value of the equation of time (eot) in minutes
		- \"eot_vecor\": A vector with the day as x and the eot value as y
		- \"declination\": The declination (vertical angle) of the plane of the elliptic
		- \"solar_noon\": The time where the sun stands highes (true noon) in minutes
		- \"sunrise\": Time of the sunrise in minutes
		- \"sunset\": Time of the sunset in minutes
		- \"is_summertime\": If (roughly) is summertime -> +1 hour (based on year 2022 in Germany)
	
	Be Aware: The metrics are approximations and are calculated for UTC-0 
				and do not reflect to summer/winter-time.
	
	Parameters:
		_day: number of day starting from the first day in the year.
	
	returns a Dictionary containing all the values
	\"\"\"
	var day = fposmod(_day + year_start_day_offset, days_per_year)
	
	var year_pct = fposmod(day, days_per_year) / days_per_year
	
	var real_world_eot_value = calculate_eot(day)
	var eot_pct =  0.5 + real_world_eot_value * 60 / DEFAULT_SECONDS_PER_DAY
	var eot_value = eot_pct * day_duration / 60  - _calculate_half_day_minutes()
	
	var declination = calculate_declination(day)
	
	var real_world_solar_noon = _calculate_solar_noon_time(real_world_eot_value, declination)
	var solar_noon_pct = 0.5 + real_world_solar_noon / DEFAULT_SECONDS_PER_DAY
	var solar_noon = solar_noon_pct * day_duration / 60
	
	var real_world_sunrise = _calculate_sunrise_or_set_time(real_world_eot_value, declination, false)
	var sunrise_pct = (60 * real_world_sunrise) / DEFAULT_SECONDS_PER_DAY
	var sunrise = sunrise_pct * day_duration / 60
	
	var real_world_sunset = _calculate_sunrise_or_set_time(real_world_eot_value, declination, true)
	var sunset_pct = (60 * real_world_sunset) / DEFAULT_SECONDS_PER_DAY
	var sunset = sunset_pct * day_duration / 60
	
	var real_world_twilight_duration = _calculate_twilight_duration(real_world_eot_value, declination)
	var twilight_duration_pct = (60 * real_world_twilight_duration) / DEFAULT_SECONDS_PER_DAY
	var twilight_duration = twilight_duration_pct * day_duration / 60
	
	return {
		\"day\" : day, # The day of the eot data (actual day including day offset)
		\"year_pct\" : year_pct, # The percentage of the day in respect of the year
		\"day_duration\" : day_duration, # The duration of a day
		\"eot\" : eot_value, # The value of the equation of time (eot)
		\"eot_vector\" : Vector2(day, eot_value), # A vector of the day and eot
		\"eot_hour_time\" : planet_minutes_to_hours(_calculate_half_day_minutes() + eot_value),
		\"declination\" : declination, # The declination (of the sun at that time)
		\"solar_noon\": solar_noon, # Time of midday
		\"sunrise\" : sunrise, # Time of sunrise
		\"sunset\" : sunset, # Time of sunset
		\"twilight_duration\" : twilight_duration, # Duration of twilight_duration (starts after sunrise/before sunset)
		# Pcts respectively to day_length in seconds
		\"eot_pct\" : eot_pct, # EOT in pct
		\"one_hour_pct\" : day_duration / (HOURS_PER_DAY * MINUTES_PER_HOUR), # Pct of the duration of one hour
		\"solar_noon_pct\" : solar_noon_pct, # Pct of solar noon
		\"sunrise_pct\" : sunrise_pct, # Pct of sunrise
		\"sunset_pct\" : sunset_pct, # Pct of sunset
		\"twilight_duration_pct\" : twilight_duration_pct, # Pct of twilight duration
		# Is Summertime
		\"is_summertime\" : (year_pct >= SUMMER_TIME_BEGIN_PCT and year_pct <= SUMMER_TIME_END_PCT) # If is summer time (based on 2022)
	}


func calculate_eot(_day : float, _prevent_leap_year_offset : bool = true) -> float:
	\"\"\"
	Calculates an approximation of the equation of time (eot) of a planet with custom day and year length.
	
	Be Aware: The metrics are approximations, are calculated using UTC-0 
				and do not reflect to summer/winter-time.
	
	Parameters:
		_day: number of day starting from the first day in the year.
		_prevent_leap_year_offset: prevents offsets in the eot that would need to be compensated with a leap year.
	
	returns the eot in minutes for the given day and specified planets orbit.
	\"\"\"
	
	return _calculate_eot_or_declination(_day, true, _prevent_leap_year_offset)


func calculate_declination(_day : float, _prevent_leap_year_offset : bool = true) -> float:
	\"\"\"
	Calculates an approximation of the declination of a planet with custom day and year length.
	
	Be Aware: The metrics are approximations, are calculated using UTC-0 
				and do not reflect to summer/winter-time.
	
	Parameters:
		_day: number of day starting from the first day in the year.
		_prevent_leap_year_offset: prevents offsets in the eot that would need to be compensated with a leap year.
	
	returns the declination in degrees for the given day and specified planets orbit.
	\"\"\"
	
	return _calculate_eot_or_declination(_day, false, _prevent_leap_year_offset)


func _calculate_eot_or_declination(_day : float, _return_eot : bool, _prevent_leap_year_offset : bool = true) -> float:
	\"\"\"
	Calculates an approximation of the equation of time (eot) or declination of a planet with custom day and year length.
	EOT is the difference of the the local time and mean local time at that date.
	It can be further used to calculate like sunrise or sunset
	
	The planet has an orbit with dimensions (orbit_dimension_a, orbit_dimension_b) 
	aswell as the given obliquity. 
	
	The default values produce values very similar to the earth.
	
	Parameters:
		_day: number of day starting from the first day in the year.
		_return_eot: if true returns the value for the eot, if false the declination on that day
		_prevent_leap_year_offset: prevents offsets in the eot that would need to be compensated with a leap year.
	
	returns the eot in minutes or the declination in degrees for the given day and specified planets orbit.
	\"\"\"
	# Based an approximate implementation from \"The Latitude and Longitude of the Sun\" by David Williams
	var d = _day
	
	if (_prevent_leap_year_offset):
		d = fmod(d, int(days_per_year)) # Prevents (leap year) offset
	
	var e = _calculate_eccentricity(orbit_dimension_a, orbit_dimension_b)
	
	var w = deg_to_rad(360.0 / days_per_year) # Average Angle Speed per day
	
	var year_length_factor = (DAYS_IN_JULIAN_CALENDER_YEAR / days_per_year)
	
	# 10 days from December solstice to New Year (Jan 1) scaled from the real earth year
	var solstice_offset = 10.0 * year_length_factor
	# 12 days from December solstice to perihelion scaled from the real earth year
	var solice_perhelion_offset = 12.0 * w * year_length_factor
	
	var a = w * (d + solstice_offset)
	var b = a + 2 * e * sin(a - solice_perhelion_offset)
	
	if _return_eot:
		var c = (a - atan(tan(b) / cos(deg_to_rad(obliquity)))) / PI
		return REAL_WORLD_HALF_DAY_MINUTES * (c - round(c)) # Eot in minutes
	else:
		var c = deg_to_rad(obliquity) * cos(b)
		return rad_to_deg(atan(c / sqrt(1 - pow(c, 2)))) # Declination in degrees


func _calculate_solar_noon_time(_eot_value : float, _declination : float) -> float:
	\"\"\"
	Calculates the solar noon time (true midday) from the eot and declination on that day.
	
	Parameters:
		_eot_value: The eot value of a day.
		_declination: The declination of a day.
	
	returns the time solar noon time (true midday) (in planet minutes)
	\"\"\"
	
	return REAL_WORLD_HALF_DAY_MINUTES - 4 * longitude - _eot_value


func _calculate_sunrise_or_set_time(_eot_value : float, _declination : float, _is_sunset : bool) -> float:
	\"\"\"
	Calculates the time of the sunset or sunrise from the eot and declination on that day.
	
	Parameters:
		_eot_value: The eot value of a day.
		_declination: The declination of a day.
		_is_sunset: if true returns the time of sunset, else the sunrise.
	
	returns the time for sunrise or sunset (in planet minutes)
	\"\"\"
	
	return _calculate_time_of_zenit_angle(SUNRISE_SUNSET_ZENIT, _eot_value, _declination, _is_sunset)


func _calculate_twilight_duration(_eot_value : float, _declination : float) -> float:
	\"\"\"
	Calculates the time of the dawn from the eot and declination on that day.
	
	Parameters:
		_eot_value: The eot value of a day.
		_declination: The declination of a day.
		_is_dusk: if true returns the time of sunset, else the sunrise.
	
	returns the duration of the twilight on that day (in planet minutes)
	\"\"\"
	
	var dawn_start_zenit = SUNRISE_SUNSET_ZENIT
	var dawn_end_zenit = SUNRISE_SUNSET_ZENIT - CIVIL_TWILIGHT_ZENIT_OFFSET

	var dawn_start_time = _calculate_time_of_zenit_angle(dawn_start_zenit, _eot_value, _declination, false)
	var dawn_end_time = _calculate_time_of_zenit_angle(dawn_end_zenit, _eot_value, _declination, false)
	
	return abs(dawn_end_time - dawn_start_time)


func _calculate_time_of_zenit_angle(_zenit : float, _eot_value : float, _declination : float, _is_evening : bool) -> float:
	\"\"\"
	Calculates the time of the sunset or sunrise from the eot and declination on that day.
	
	Parameters:
		_zenit : angle for the sun to reach.
		_eot_value: The eot value of a day.
		_declination: The declination of a day.
		_is_sunset: if true returns the time of sunset, else the sunrise.
	
	returns the time to reach sunrise's or sunset's angle (in planet minutes)
	\"\"\"
	
	var angle_dir = pow(-1, int(_is_evening))
	var zenit = deg_to_rad(_zenit)
	var lat = deg_to_rad(latitude)
	var decl = deg_to_rad(_declination)
	
	var hour_angle = angle_dir * rad_to_deg(acos(cos(zenit) / (cos(lat) * cos(decl)) - tan(lat) * tan(decl)))
	
	return REAL_WORLD_HALF_DAY_MINUTES - 4 * (longitude + hour_angle) - _eot_value


func _calculate_eccentricity(_a : float, _b : float) -> float:
	\"\"\"
	Calculates the eccentricity of an ellipse with dimensions a and b.
	
	Parameters:
		_a: first dimension of the ellispe (e.g. width)
		_b: second dimension of the ellispe (e.g. heigh)
	
	returns eccentricity of an ellipse
	\"\"\"
	
	var semi_major = max(_a, _b)
	var semi_minor = min(_a, _b)
	
	return (semi_major - semi_minor) / (semi_major + semi_minor)


func _calculate_half_day_minutes() -> float:
	\"\"\"
	Calculates the duration of minutes in hald a day:
	
	12hours a 60 minutes = 720 minutes for the real world, then scale acordignly
	
	returns the duration of minutes in half a day
	\"\"\"
	
	return REAL_WORLD_HALF_DAY_MINUTES * get_planet_time_scale()


func get_all_values_till(_end_day : int) -> Array:
	\"\"\"
	Returns a list of all dates till _end_day
	
	Parameters:
		_end_day: the last day (exclusive) for which values are calculated
		_plotable: if plotable the y values will be negated to be plotted in a normal graph
	
	returns list of eot values for each day
	\"\"\"
	
	return get_all_values_between(0, _end_day)


func get_all_values_between(_start_day : int, _end_day : int) -> Array:
	\"\"\"
	Returns a list of all dates between _start_day and _end_day
	
	Parameters:
		_start_day: the first day (inclusive) for which a value is calculated
		_end_day: the last day (exclusive) for which values are calculated
		_plotable: if plotable the y values will be negated to be plotted in a normal graph
	
	returns list of eot values for each day
	\"\"\"
	
	var values = []
	for i in range(_start_day, _end_day):
		values.append(get_full_info_about_day(i))
	return values


func get_eot_values_till(_end_day : int) -> Array:
	\"\"\"
	Returns a list the eot of all dates till _end_day
	
	Parameters:
		_end_day: the last day (exclusive) for which values are calculated
		_plotable: if plotable the y values will be negated to be plotted in a normal graph
	
	returns list of eot values for each day
	\"\"\"
	
	return get_eot_values_between(0, _end_day)


func get_eot_values_between(_start_day : int, _end_day : int) -> Array:
	\"\"\"
	Returns a list of all dates between _start_day and _end_day
	
	Parameters:
		_start_day: the first day (inclusive) for which a value is calculated
		_end_day: the last day (exclusive) for which values are calculated
		_plotable: if plotable the y values will be negated to be plotted in a normal graph
	
	returns list of eot values for each day
	\"\"\"
	
	var values = []
	for i in range(_start_day, _end_day):
		values.append(Vector2(i, calculate_eot(i)))
	return values


func planet_seconds_to_minutes(_seconds : float) -> float:
	\"\"\"
	Converts planet-seconds to planet-minutes (no real seconds/minutes!!)
	
	Parameters:
		_seconds: planet seconds to be converted in planet minutes
	
	returns the planet minutes
	\"\"\"
	
	return _seconds * get_planet_time_scale() / SECONDS_PER_MINUTE


func planet_minutes_to_seconds(_minutes : float) -> float:
	\"\"\"
	Converts planet-minutes to planet-seconds (no real seconds/minutes!!)
	
	Parameters:
		_minutes: planet minutes to be converted in planet seconds
	
	returns the planet seconds
	\"\"\"
	
	return _minutes / get_planet_time_scale() * SECONDS_PER_MINUTE


func planet_hours_to_minutes(_hour : float) -> float:
	\"\"\"
	Converts planet-hour to planet-minutes (no real hours/minutes!!)
	
	Parameters:
		_hour: planet hours to be converted in planet hours
	
	returns the planet minutes
	\"\"\"
	
	return _hour * get_planet_time_scale() * MINUTES_PER_HOUR


func planet_minutes_to_hours(_minutes : float) -> float:
	\"\"\"
	Converts planet-hour to planet-minutes (no real hours/minutes!!)
	
	Parameters:
		_minutes: planet minutes to be converted in planet hours
	
	returns the planet hours
	\"\"\"
	
	return _minutes * get_planet_time_scale() / MINUTES_PER_HOUR


func get_planet_time_scale() -> float:
	\"\"\"
	Calculates and returns the scale of real-world time to planet time
	
	returns the planet's time scale
	\"\"\"
	return day_duration / DEFAULT_SECONDS_PER_DAY
"

[node name="EOTPlot" type="Control"]
layout_mode = 3
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2
script = SubResource("GDScript_q3d7w")

[node name="Background" type="ColorRect" parent="."]
layout_mode = 0
anchor_right = 1.0
anchor_bottom = 1.0
color = Color(0, 0, 0, 1)

[node name="Margin" type="MarginContainer" parent="."]
layout_mode = 1
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2
theme_override_constants/margin_left = 10
theme_override_constants/margin_top = 10
theme_override_constants/margin_right = 10
theme_override_constants/margin_bottom = 10

[node name="GridContainer" type="GridContainer" parent="Margin"]
layout_mode = 2
columns = 2

[node name="YControl" type="HBoxContainer" parent="Margin/GridContainer"]
layout_mode = 2
theme_override_constants/separation = 16
alignment = 2

[node name="YLabel" type="Label" parent="Margin/GridContainer/YControl"]
unique_name_in_owner = true
layout_mode = 2
text = "YLabel"

[node name="Margin" type="MarginContainer" parent="Margin/GridContainer/YControl"]
unique_name_in_owner = true
layout_mode = 2
size_flags_vertical = 3
theme_override_constants/margin_right = 4

[node name="YTicks" type="VBoxContainer" parent="Margin/GridContainer/YControl/Margin"]
unique_name_in_owner = true
layout_mode = 2
size_flags_vertical = 3
alignment = 1

[node name="Label" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "4"

[node name="Control" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label2" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "3"

[node name="Control2" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label3" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "2"

[node name="Control3" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label4" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "1"

[node name="Control4" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label5" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "0"

[node name="Control5" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label6" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "-1"

[node name="Control6" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label7" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "-2"

[node name="Control7" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label8" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "-3"

[node name="Control8" type="Control" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
size_flags_vertical = 3

[node name="Label9" type="Label" parent="Margin/GridContainer/YControl/Margin/YTicks"]
layout_mode = 2
text = "-4"

[node name="Margin" type="MarginContainer" parent="Margin/GridContainer"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3
theme_override_constants/margin_left = 10
theme_override_constants/margin_top = 10
theme_override_constants/margin_right = 10
theme_override_constants/margin_bottom = 10

[node name="PlotsBackground" type="ColorRect" parent="Margin/GridContainer/Margin"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3
color = Color(0.168627, 0.168627, 0.168627, 1)

[node name="PlotOutline" type="Line2D" parent="Margin/GridContainer/Margin"]
unique_name_in_owner = true

[node name="Spacer" type="Control" parent="Margin/GridContainer"]
custom_minimum_size = Vector2(200, 0)
layout_mode = 2

[node name="Center" type="CenterContainer" parent="Margin/GridContainer/Spacer"]
layout_mode = 1
anchors_preset = 15
anchor_right = 1.0
anchor_bottom = 1.0
grow_horizontal = 2
grow_vertical = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="PlotOptionDropdown" type="OptionButton" parent="Margin/GridContainer/Spacer/Center"]
unique_name_in_owner = true
layout_mode = 2

[node name="XControl" type="VBoxContainer" parent="Margin/GridContainer"]
layout_mode = 2
size_flags_horizontal = 3

[node name="Margin" type="MarginContainer" parent="Margin/GridContainer/XControl"]
layout_mode = 2
size_flags_horizontal = 3

[node name="XTicks" type="HBoxContainer" parent="Margin/GridContainer/XControl/Margin"]
unique_name_in_owner = true
layout_mode = 2
size_flags_horizontal = 3
alignment = 1

[node name="Label" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "0"

[node name="Control" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label2" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "1"

[node name="Control2" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label3" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "2"

[node name="Control3" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label4" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "3"

[node name="Control4" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label5" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "4"

[node name="Control5" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label6" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "5"

[node name="Control6" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label7" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "6"

[node name="Control7" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label8" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "7"

[node name="Control8" type="Control" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
size_flags_horizontal = 3
size_flags_vertical = 3

[node name="Label9" type="Label" parent="Margin/GridContainer/XControl/Margin/XTicks"]
layout_mode = 2
text = "8"

[node name="XLabel" type="Label" parent="Margin/GridContainer/XControl"]
unique_name_in_owner = true
layout_mode = 2
text = "XLabel"
horizontal_alignment = 1

[node name="CustomEOT" type="Node" parent="."]
script = SubResource("GDScript_gwx40")

[connection signal="item_selected" from="Margin/GridContainer/Spacer/Center/PlotOptionDropdown" to="." method="_on_PlotOptionDropdown_item_selected"]
